class QueryQMeshEvent : ZilchEvent
{
  var QMesh : QMesh;
}

class GaussMapVisualizer : ZilchComponent
{
  [Property] var Active : Boolean = true;
  [Property] var TargetPath : CogPath = CogPath();
  [Property] var Negate : Boolean = false;
  [Property] var DrawOriginalFaceNormals : Boolean = false;
  
  [Property] var EdgeColor : Real4 = Colors.Blue;
  [Property] var VertexColor : Real4 = Colors.Red;
  [Property] var VertexSize : Real = 0.01;
  
  var Vertices : Array[Real3] = Array[Real3]();
  var Edges : Array[Integer2] = Array[Integer2]();
  
  var TargetCenter : Real3 = Real3();
  sends QueryQMesh : QueryQMeshEvent;
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(init, Events.AllObjectsInitialized, this.OnAllObjectsInitialized);
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
  }

  function OnAllObjectsInitialized(event : CogInitializerEvent)
  {
    this.Run();
  }
  
  function Run()
  {
    var qHull = this.TargetPath.QuickHullMeshRenderer;
    if(qHull == null)
      return;
    qHull.Run();
  }
  
  function Compute()
  {
    var transform = this.TargetPath.Transform;
    this.TargetCenter = transform.WorldTranslation;
    
    // Query the target cog for a mesh
    var targetCog = this.TargetPath.Cog;
    var toSend = QueryQMeshEvent();
    targetCog.DispatchEvent(Events.QueryQMesh, toSend);
    // If we have a mesh then draw the gauss map
    if(toSend.QMesh != null)
      this.Compute(toSend.QMesh, transform);
  }
  
  function Compute(mesh : QMesh, transform : Transform)
  {
    this.Vertices.Clear();
    this.Edges.Clear();
    
    foreach(var faceNormal in mesh.FaceNormals)
    {
      faceNormal = Math.Normalize(transform.TransformNormal(faceNormal));
      if(this.Negate)
        faceNormal *= -1;
      this.Vertices.Add(faceNormal);
    }
    foreach(var edge in mesh.UniqueEdges)
    {
      var twin = mesh.Edges[edge.Twin];
      this.Edges.Add(Integer2(edge.Face, twin.Face));
    }
  }
  
  function Draw()
  {
    this.Compute();
    
    var center = this.Owner.Transform.WorldTranslation;
    var radius = 0.51;
    foreach(var vertex in this.Vertices)
    {
      var point = center + radius * vertex;
      if(this.DrawOriginalFaceNormals)
        DebugDraw.Add(DebugLine(this.TargetCenter, this.TargetCenter + vertex)  {Color = this.VertexColor, HeadSize = 0.05});
      DebugDraw.Add(DebugLine(center, point));
      DebugDraw.Add(DebugSphere(point, this.VertexSize) {Color = this.VertexColor});
    }
    
    foreach(var edge in this.Edges)
    {
      var p0 = this.Vertices[edge.X];
      var p1 = this.Vertices[edge.Y];
      this.DrawArc(p0, p1, radius);
    }
  }
  
  function DrawArc(p0 : Real3, p1 : Real3, radius : Real)
  {
    var center = this.Owner.Transform.WorldTranslation;
    
    var mid = Math.Slerp(p0, p1, 0.5);
    p0 = p0 * radius + center;
    p1 = p1 * radius + center;
    mid = mid * radius + center;
    DebugDraw.Add(DebugArc(p0, mid, p1) { Color = this.EdgeColor});
  }
  
  function OnLogicUpdate(event : UpdateEvent)
  {
    if(this.Active)
      this.Draw();
  }
}

class CombinedGaussMapVisualizer : ZilchComponent
{
  var DrawAInternal : Boolean = true;
  var DrawBInternal : Boolean = true;
  var DrawDiffInternal : Boolean = true;
  
  [Property] var DrawA : Boolean
  {
    get { return this.DrawAInternal; }
    set { this.DrawAInternal = value; this.Update(this.PathA, value); }
  }
  [Property] var DrawB : Boolean
  {
    get { return this.DrawBInternal; }
    set { this.DrawBInternal = value; this.Update(this.PathB, value); }
  }
  [Property] var DrawDiff : Boolean
  {
    get { return this.DrawDiffInternal; }
    set { this.DrawDiffInternal = value; this.Update(this.PathDiff, value); }
  }
  
  [Property][Group("Internal")] var PathA : CogPath = CogPath("ObjA");
  [Property][Group("Internal")] var PathB : CogPath = CogPath("ObjB");
  [Property][Group("Internal")] var PathDiff : CogPath = CogPath("Diff");
  
  function Update(path : CogPath, value : Boolean)
  {
    var visualizer = path.GaussMapVisualizer;
    if(visualizer != null)
      visualizer.Active = value;
  }
}
