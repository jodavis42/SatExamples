class SatTester : ZilchComponent
{
  [Property] var Shape0Path = CogPath();
  [Property] var Shape1Path = CogPath();
  
  [Property] var MinkowskiDiffCogPath = CogPath();
  // What SAT axis to debug draw / print. This might be a face or an edge axis
  [Property] var TargetTestIndex : Integer = 0;
  
  [Property] var DrawAllEdges : Boolean = true;
  [Property] var EarlyOut : Boolean = false;
  [Property] var DebugDrawEdgeFace : Boolean = false;
  
  // Used to change the gauss map sphere's color based upon if the edges intersect
  [ShaderInput] var AlbedoValue : Real4 = Colors.DimGray;
  
  // What SAT axis we have currently run.
  var TestedAxesA : Integer;
  var TestedAxesB : Integer;
  var CurrentTestAxisIndex : Integer = 0;
  var ValidAxisIndex : Integer = 0;
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    Zero.Connect(Zero.Keyboard, Events.KeyUp, this.OnKeyUp);
  }

  function OnKeyUp(event : KeyboardEvent)
  {
    if(event.Key == Keys.Minus || event.Key == Keys.Subtract)
      --this.TargetTestIndex;
    if(event.Key == Keys.Equal || event.Key == Keys.Add)
      ++this.TargetTestIndex;
  }
  
  function UpdateMinkowskiAxis()
  {
    var targetFaceOverride = this.MinkowskiDiffCogPath.TargetFaceOverride;
    if(targetFaceOverride != null)
      targetFaceOverride.TargetFaceIndex = this.ValidAxisIndex;
  }

  function OnLogicUpdate(event : UpdateEvent)
  {
    var poly0 = this.Shape0Path.PolyedronShape;
    var poly1 = this.Shape1Path.PolyedronShape;
    if(poly0 == null || poly1 == null)
      return;
    
    var result = this.Run(poly0, poly1);
  }
  
  function Run(poly0 : PolyedronShape, poly1 : PolyedronShape) : Boolean
  {
    this.TestedAxesA = 0;
    this.TestedAxesB = 0;
    this.CurrentTestAxisIndex = 0;
    this.ValidAxisIndex = -1;
    this.UpdateMinkowskiAxis();
    this.ValidAxisIndex = 0;
    this.AlbedoValue = Colors.DimGray;
    
    poly0.Owner.TargetFaceOverride.TargetFaceIndex = -1;
    poly1.Owner.TargetFaceOverride.TargetFaceIndex = -1;
    
    var satTest = SatTest();
    satTest.EarlyOut = this.EarlyOut;
    satTest.EdgeLogCallback = this.EdgeLogCallback;
    satTest.FaceLogCallback = this.FaceLogCallback;
    return satTest.Test(poly0, poly1);
  }
  
  function FaceLogCallback(logInfo : SatFaceLogInfo)
  {
    if(this.CurrentTestAxisIndex == this.TargetTestIndex)
    {
      var faceIndex = this.TestedAxesA;
      if(logInfo.Poly.Owner != this.Shape0Path.Cog)
        faceIndex = this.TestedAxesB;
      
      this.UpdateMinkowskiAxis();
      var satFace = logInfo.Face;
      var mesh = logInfo.Poly.Mesh;
      var face = mesh.Faces[logInfo.FaceIndex];
      var transform = logInfo.Poly.Owner.Transform;
      
      // Change the color based upon if the objects are separating on this axis
      var color = Colors.Red;
      if(logInfo.Separation > 0)
        color = Colors.Blue;
        
      var faceOverride = logInfo.Poly.Owner.TargetFaceOverride;
      faceOverride.TargetFaceIndex = faceIndex;
      faceOverride.TargetFaceColor = color;
        
      // Draw the face normal at the center of the face
      var faceInfo = mesh.ComputeNormalAndCenter(face);
      var worldNormal = transform.TransformNormal(faceInfo.Normal);
      var worldCenter = transform.TransformPoint(faceInfo.Center);
      DebugDraw.Add(DebugLine(worldCenter, worldCenter + worldNormal) {HeadSize = 0.05, Color = color});
      
      // Draw the vertex on the gauss map
      this.DrawVertex(worldNormal, Colors.Black);
    }
    
    if(logInfo.Poly.Owner == this.Shape0Path.Cog)
      ++this.TestedAxesA;
    else
      ++this.TestedAxesB;
      
    ++this.ValidAxisIndex;
    ++this.CurrentTestAxisIndex;
  }
  
  function EdgeLogCallback(logInfo : SatEdgeLogInfo)
  {
    if(!logInfo.IsMinkowski && this.DrawAllEdges == false)
      return;
      
    if(this.CurrentTestAxisIndex == this.TargetTestIndex)
    {
      if(logInfo.IsMinkowski)
      {
        this.AlbedoValue = Colors.White;
        this.UpdateMinkowskiAxis();
      }
      else
        this.AlbedoValue = Colors.DimGray;
        
      var radius = 0.51;
      var edge0 = logInfo.Edge0;
      var edge1 = logInfo.Edge1;
      
      // Draw the edges on the original shapes
      DebugDraw.Add(DebugLine(edge0.Vertex0, edge0.Vertex1) {Color = Colors.Red});
      DebugDraw.Add(DebugLine(edge1.Vertex0, edge1.Vertex1) {Color = Colors.Blue});
      
      // Draw obj0's edge gauss map
      this.DrawVertex(edge0.FaceNormal0, Colors.Red);
      this.DrawVertex(edge0.FaceNormal1, Colors.Red);
      this.DrawArc(edge0.FaceNormal0, edge0.FaceNormal1, radius, Colors.Red);
      // Draw obj1's edge gauss map
      this.DrawVertex(-edge1.FaceNormal0, Colors.Blue);
      this.DrawVertex(-edge1.FaceNormal1, Colors.Blue);
      this.DrawArc(-edge1.FaceNormal0, -edge1.FaceNormal1, radius, Colors.Blue);
      
      if(this.DebugDrawEdgeFace)
      {
        var p00 = edge0.Vertex0 - edge1.Vertex0;
        var p01 = edge0.Vertex0 - edge1.Vertex1;
        var p10 = edge0.Vertex1 - edge1.Vertex0;
        var p11 = edge0.Vertex1 - edge1.Vertex1;
        DebugDraw.Add(DebugLine(p00, p01) {OnTop = true});
        DebugDraw.Add(DebugLine(p01, p11) {OnTop = true});
        DebugDraw.Add(DebugLine(p11, p10) {OnTop = true});
        DebugDraw.Add(DebugLine(p10, p00) {OnTop = true});
      }
    }
    if(logInfo.IsMinkowski)
    {
      ++this.ValidAxisIndex;
    }
    
    ++this.CurrentTestAxisIndex;
  }
  
  function DrawArc(p0 : Real3, p1 : Real3, radius : Real, edgeColor : Real4)
  {
    p0 = Math.Normalize(p0);
    p1 = Math.Normalize(p1);
    var center = this.Owner.Transform.WorldTranslation;
    
    var mid = Math.Slerp(p0, p1, 0.5);
    p0 = p0 * radius + center;
    p1 = p1 * radius + center;
    mid = mid * radius + center;
    DebugDraw.Add(DebugArc(p0, mid, p1) { Color = edgeColor});
  }
  
  function DrawVertex(vertex : Real3, vertexColor : Real4)
  {
    vertex = Math.Normalize(vertex);
    var center = this.Owner.Transform.WorldTranslation;
    var radius = 0.51;
    var point = center + radius * vertex;
    DebugDraw.Add(DebugSphere(point, 0.01) {Color = vertexColor});
  }
}
